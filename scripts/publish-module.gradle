apply plugin: 'maven-publish'
apply plugin: 'signing'


ext {
    PUBLISH_GROUP_ID = 'com.contentstack.sdk'
    PUBLISH_ARTIFACT_ID = 'android'
    PUBLISH_VERSION = '3.12.0-SNAPSHOT'
    POM_NAME = 'contentstack-android'
    WEBSITE = 'http://www.contentstack.com'
    PUBLISH_DESCRIPTION = 'The Content Delivery SDK is used to retrieve content from your Contentstack account and deliver it to your web or mobile properties.'
    PUBLISH_URL = 'https://github.com/contentstack/contentstack-android'
    PUBLISH_LICENSE_NAME = 'MIT License'
    PUBLISH_LICENSE_URL = 'https://github.com/contentstack/contentstack-android/blob/master/LICENSE'
    PUBLISH_DEVELOPER_ID = 'ishaileshmishra'
    PUBLISH_DEVELOPER_NAME = 'Shailesh Mishra'
    PUBLISH_DEVELOPER_EMAIL = 'mobile@contentstack.com'
    PUBLISH_SCM_CONNECTION = 'scm:git@github.com:contentstack/contentstack-android'
    SCM_DEVELOPER_CONNECTION = 'scm:git@github.com:contentstack/contentstack-android.git'
    PUBLISH_SCM_URL = 'https://github.com/contentstack/contentstack-android/tree/master'
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = PUBLISH_GROUP_ID
                artifactId = PUBLISH_ARTIFACT_ID
                version = PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    //from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }
                artifact androidSourcesJar
                pom {
                    name = POM_NAME
                    description = PUBLISH_DESCRIPTION
                    url = WEBSITE

                    licenses {
                        license {
                            name = PUBLISH_LICENSE_NAME
                            url = PUBLISH_LICENSE_URL
                        }
                    }
                    developers {
                        developer {
                            id = PUBLISH_DEVELOPER_ID
                            name = PUBLISH_DEVELOPER_NAME
                            email = PUBLISH_DEVELOPER_EMAIL
                        }
                    }
                    scm {
                        connection = PUBLISH_SCM_CONNECTION
                        developerConnection = SCM_DEVELOPER_CONNECTION
                        url = PUBLISH_SCM_URL
                    }
                }
            }
        }
    }
}


ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}

